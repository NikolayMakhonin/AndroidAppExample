buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url  'http://dl.bintray.com/raphaelbussa/maven' }
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'

//for Dagger 2
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.github.nikolaymakhonin.android_app_example"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //for retrolambda
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //region Tests

    testCompile 'junit:junit:4.12'
    //see: https://github.com/mockito/mockito
    //see: https://developer.android.com/training/testing/unit-testing/local-unit-tests.html
    testCompile 'org.mockito:mockito-core:1.10.19'

    //endregion

    //region Frontend

    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:multidex:1.0.1'

    //see: https://github.com/rebus007/HeaderView
    compile 'rebus:header-view:1.1.4'

    //Destroy window animation:
    //see: https://github.com/Yalantis/StarWars.Android
    compile 'com.yalantis:starwarstiles:0.1.1'

    //Material Preference:
    //see: https://github.com/consp1racy/android-support-preference
    compile 'net.xpece.android:support-preference:0.8.1'
    // + color picker:
    compile 'net.xpece.android:support-preference-color:0.8.0'
    // + popup menu and spinner:
    compile 'net.xpece.android:support-spinner:0.8.1'

    //Material View Pager:
    //see: https://github.com/florent37/MaterialViewPager
    compile ('com.github.florent37:materialviewpager:1.1.3@aar'){
        transitive = true
    }

    //Google APIs:
    //see: https://developers.google.com/android/guides/setup#add_google_play_services_to_your_project
    //Google Actions, Base Client Library:
    compile ('com.google.android.gms:play-services-base:9.0.0') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    //Google Location, Activity Recognition, and Places:
    compile 'com.google.android.gms:play-services-location:9.0.0'
    //Google Maps:
    compile 'com.google.android.gms:play-services-maps:9.0.0'

    compile 'com.squareup.picasso:picasso:2.5.2'

    //endregion

    //region Backend

    //Retrofit:
    //see: http://square.github.io/retrofit/
    //see: http://mvnrepository.com/artifact/com.squareup.retrofit2
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'

    //Dagger 2:
    apt 'com.google.dagger:dagger-compiler:2.4'
    compile 'com.google.dagger:dagger:2.4'

    compile project(':CommonDI')

    // http://mvnrepository.com/artifact/com.squareup.okhttp/okhttp
    compile group: 'com.squareup.okhttp', name: 'okhttp', version: '2.7.5'

    //endregion
}

